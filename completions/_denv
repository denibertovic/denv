#compdef denv

kube_conf_dir=(~/.kube)

_denv () {
    local line
    _arguments -C \
        --version"[display version information]" \
        --help"[display usage information]" \
        "1: :__denv_commands" \
        "*::arg:->args" \

    case $line[1] in
        kube)
            __kube
        ;;
        pass)
            __pass
        ;;
        vault)
            __vault
        ;;
        tf)
            __tf
        ;;
        fetch)
            __fetch
        ;;
        'source')
            __source
        ;;
        aws)
            __aws
        ;;
        deactivate)
            __deactivate
        ;;
    esac
}

__fetch () {
    _arguments \
        "(-m --makefile)"{-m,--makefile=}"[makefile template]" \
}

__source () {
    _alternative \
        "files:filename:_files" \
}

__tf() {
    _arguments \
        "(-p --terraform-project)"{-p,--terraform-project=}"[Terraform env file path]:project:_files" \
}


__deactivate () {}

__pass() {
    _arguments \
        "(-p --password-store-path)"{-p,--password-store-path=}"[Password store path]:path:_files" \
}

__vault() {
    _arguments \
        "(-p --vault-project)"{-p,--vault-project=}"[Vault env file path]:path:_files" \
}

__aws() {
    _arguments \
        "(-p --profile)"{-p,--profile=}"[AWS profile to use]" \
}

__kube_namespaces=(default kube-system monitoring review)

__kube() {
    _arguments \
        "(-p --kube-project)"{-p,--kube-project=}"[Kube project]:project:_files" \
        "(-n --kube-namespace)"{-n,--kube-namespace=}"[Kube namespace]:namespace:($__kube_namespaces)" \
        "(-t --tiller-namespace)"{-t,--tiller-namespace=}"[Tiller namespace]:namespace:($__kube_namespaces)" \
}

__denv_commands () {
    _values \
        "subcommand" \
        "kube[Set kube environment]" \
        "pass[Set pass environment]" \
        "source[Source raw env file]" \
        "vault[Set vault environment]" \
        "tf[Set terraform environment]" \
        "aws[Set AWS profile to use]" \
        "deactivate[Deactivate environment]"
}

_denv "$@"

